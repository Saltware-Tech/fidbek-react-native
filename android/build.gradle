buildscript {
  ext.kotlin_version = "1.9.24"
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.5.2"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def isNewArchitectureEnabled() {
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (!isNewArchitectureEnabled()) {
  throw new GradleException(
    "@fidbek/react-native requires React Native New Architecture. " +
      "Set newArchEnabled=true in android/gradle.properties."
  )
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.facebook.react"

android {
  namespace "com.fidbek.reactnative"
  compileSdkVersion safeExtGet("compileSdkVersion", 34)

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 24)
  }

  sourceSets {
    main {
      java.srcDirs += ["src/main/java", "src/newarch"]
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  implementation files("libs/fidbek-android.aar")
  implementation "com.facebook.react:react-android"
  implementation "androidx.core:core-ktx:1.13.1"
  implementation "androidx.appcompat:appcompat:1.7.0"
  implementation "com.google.android.material:material:1.12.0"
  implementation "com.squareup.okhttp3:okhttp:4.12.0"
}

react {
  jsRootDir = file("../")
  libraryName = "FidbekReactNative"
  codegenJavaPackageName = "com.fidbek.reactnative"
}
